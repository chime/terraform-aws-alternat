## Migration guide for v0.2.0

The v0.2.0 release changes the Terraform module inputs in order to associate a route table with a set of private and public subnets and an availability zone. Follow the steps below to migrate to this version. The migration can be completed without interrupting or terminating the NAT instances.

The modules accept a new input called `var.vpc_az_maps`, a list of objects mapping route tables to their corresponding subnets and AZs:

```
variable "vpc_az_maps" {
  description = "A map of az to private route tables that the NAT instances will manage."
  type = list(object({
    az                 = string
    private_subnet_ids = list(string)
    public_subnet_id   = string
    route_table_ids    = list(string)
  }))
}
```

Previouly, using the alternat module with the open source [`terraform-aws-vpc` module](https://github.com/terraform-aws-modules/terraform-aws-vpc) looked something like this:

```
module "alternat" {
  source = "git@github.com:chime/terraform-aws-alternat.git//modules/terraform-aws-alternat?ref=v0.1.3"

  alternat_image_uri         = "012345678901.dkr.ecr.us-west-2.amazonaws.com/alternat"
  alternat_image_tag         = "v0.1.3"
  ingress_security_group_ids = [aws_security_group.client_sg.id]
  private_route_table_ids    = module.vpc.private_route_table_ids
  vpc_private_subnet_ids     = module.vpc.private_subnets
  vpc_public_subnet_ids      = module.vpc.public_subnets
  vpc_id                      = module.vpc.vpc_id
}
```

With `vpc_az_maps`, the module call now looks like:

```
data "aws_subnet" "subnet" {
  count = length(module.vpc.private_subnets)
  id    = module.vpc.private_subnets[count.index]
}

locals {
  vpc_az_maps = [
    for index, rt in module.vpc.private_route_table_ids
    : {
      az                 = data.aws_subnet.subnet[index].availability_zone
      route_table_ids    = [rt]
      public_subnet_id   = module.vpc.public_subnets[index]
      private_subnet_ids = [module.vpc.private_subnets[index]]
    }
  ]
}

module "alternat" {
  source = "git@github.com:chime/terraform-aws-alternat.git//modules/terraform-aws-alternat?ref=v0.2.0"

  alternat_image_uri         = "188238883601.dkr.ecr.us-west-2.amazonaws.com/alternat"
  alternat_image_tag         = "v0.2.0"
  ingress_security_group_ids = [aws_security_group.client_sg.id]
  vpc_az_maps                 = local.vpc_az_maps
  vpc_id                      = module.vpc.vpc_id
}
```

In the code above, the `availability_zone` of the `aws_subnet` data source will not resolve until the VPC is created. Therefore, for new deployments where the VPC and alterNAT are being set up for the first time, an error will occur stating, `Invalid for_each argument` error because "Terraform cannot predict how many instances will be created." To work around this, follow the advice in the error message by using the `-target` argument to first create the VPC. Users with existing VPCs/subnets are not impacted by this.

After making the above changes, running `terraform plan` will show some resources as changed and others as replaced. You can avoid the important replacements by using `moved` blocks. For example, in the `us-west-2` region:

```
moved {
  from = module.alternat_instances.aws_nat_gateway.main[0]
  to   = module.alternat_instances.aws_nat_gateway.main["us-west-2a"]
}
moved {
  from = module.alternat_instances.aws_eip.nat_gateway_eips[0]
  to   = module.alternat_instances.aws_eip.nat_gateway_eips["us-west-2a"]
}
moved {
  from = module.alternat_instances.aws_autoscaling_group.nat_instance[0]
  to   = module.alternat_instances.aws_autoscaling_group.nat_instance["us-west-2a"]
}
```

You'll need to repeat the above blocks for each availability zone. The Lambda functions and CloudWatch event targets will still be replaced, but this will not cause any downtime for the NAT instances or NAT gateways.
